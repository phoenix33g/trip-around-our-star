<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/xhtml;charset=UTF-8" />
    <meta http-equiv="Content-Style-Type" content="text/css" />
    <meta http-equiv="Content-Language" content="en" />
    <link rel="stylesheet" href="Script.css" type="text/css" />
    <title>Viz 3 Script - Examples</title>
  </head>


<body>

<a href="ScriptMain.html">Viz 3 Script</a>

<h2>Viz 3 Script - Examples</h2>

<h3>Tetris</h3>
<pre class="code">
dim HEIGHT, WIDTH, TOTAL, NUM_BRICKS as integer
dim currentBrick, currentRot, counter as integer
dim xpos, ypos as integer ' position of the falling brick

dim squares as Array[integer]
dim squareContainers as Array[Container]
dim bricks as Array[string]

HEIGHT = 15
WIDTH = 9
TOTAL = HEIGHT * WIDTH

squares.size = TOTAL

sub OnInit()
    dim i, j as integer

    ' 0100
    ' 0100
    ' 0100
    ' 0100

    bricks.push("0100010001000100")
    bricks.push("0000111100000000")
    bricks.push("0100010001000100")
    bricks.push("0000111100000000")

    ' 0000
    ' 0000
    ' 0110
    ' 0110

    bricks.push("0000000001100110")
    bricks.push("0000000001100110")
    bricks.push("0000000001100110")
    bricks.push("0000000001100110")

    ' 0000
    ' 0100
    ' 0110
    ' 0010

    bricks.push("0000010001100010")
    bricks.push("0000000001101100")
    bricks.push("0000010001100010")
    bricks.push("0000000001101100")

    ' 0000
    ' 0010
    ' 0110
    ' 0100

    bricks.push("0000001001100100")
    bricks.push("0000000011000110")
    bricks.push("0000001001100100")
    bricks.push("0000000011000110")

    ' 0000
    ' 0110
    ' 0010
    ' 0010

    bricks.push("0000011000100010")
    bricks.push("0000001011100000")
    bricks.push("0100010001100000")
    bricks.push("0000011101000000")

    ' 0000
    ' 0110
    ' 0100
    ' 0100

    bricks.push("0000011001000100")
    bricks.push("0000111000100000")
    bricks.push("0010001001100000")
    bricks.push("0000010001110000")

    ' 0000
    ' 0000
    ' 1110
    ' 0100

    bricks.push("0000000011100100")
    bricks.push("0000010011000100")
    bricks.push("0000010011100000")
    bricks.push("0000010001100100")

    NUM_BRICKS = bricks.size / 4

    deleteGeometry
    update
    deleteChildren

    dim blocksGroup as Container
    blocksGroup = addContainer(TL_DOWN)

    open = false

    for i=0 to HEIGHT-1
        for j=0 to WIDTH-1
            dim b as Container
            if i &gt;= 3 then
                b = blocksGroup.addContainer(TL_DOWN)
                b.createMaterial
                b.name = "f_" &amp; j &amp; "_" &amp; i
                b.createGeometry("BUILT_IN*GEOM*Cube")
                b.scaling.xyz = 0.1
                b.position.x = (j-WIDTH/2.0+0.5) * 10.0
                b.position.y = - (i-HEIGHT/2.0-1.0) * 10.0
            end if
            squareContainers.push(b)
        next
    next

    initGame
end sub

sub display()
    dim i, j, index, fb as integer
    dim fc as Container

    for i=3 to HEIGHT-1
        for j=0 to WIDTH-1
            index = i * WIDTH + j
            fb = squares[index]
            fc = squareContainers[index]
            if fb = 0 then
                fc.active = false
            else
                fc.active = true
                if fb = 1 then
                    fc.material.diffuse = CColor(60.0, 60.0, 60.0)
                elseif fb = 3 then
                    fc.material.diffuse = CColor(30.0, 30.0, 30.0)
                else
                    fc.material.diffuse = CColor(40.0, 0.0, 80.0)
                end if
            end if
        next
    next
end sub

sub initGame()
    dim i as integer

    for i=0 to TOTAL-1
        squares[i] = 0
    next

    for i=0 to HEIGHT-1
        squares[WIDTH*i] = 3
        squares[WIDTH*i + WIDTH-1] = 3
    next

    for i=TOTAL-WIDTH to TOTAL-1
        squares[i] = 3
    next

    newBrick
    display
end sub

sub newBrick()
    xpos = WIDTH / 2 - 2
    ypos = -1
    counter = 0
    currentBrick = random(NUM_BRICKS)
    currentRot = 0
end sub

sub clear2()
    ' clear the squares occupied by the falling brick
    dim i as integer
    for i=0 to TOTAL-1
        if squares[i] = 2 then
            squares[i] = 0
        end if
    next
end sub

function drawBrick(x as integer, y as integer, rot as integer, fill as integer) as boolean
    dim b as string
    dim index, i, xb, yb, x2, y2 as integer

    clear2
    b = bricks[currentBrick * 4 + rot]
    for i=0 to 15
        xb = i mod 4
        yb = i / 4
        if b.getChar(i) = "1" then
            x2 = x + xb
            y2 = y + yb
            if y2 &gt;= 0 then
                index = y2 * WIDTH + x2
                if x2 &lt; 0 or x2 &gt;= WIDTH or y2 &gt;= HEIGHT or squares[index] &lt;&gt; 0 then
                    drawBrick = false ' collision
                    exit function
                elseif y2 &gt;= 3 or fill &lt;&gt; 1 then
                    squares[index] = fill
                end if
            end if
        end if
    next

    drawBrick = true ' no collision
end function

function isLineFilled(line as integer) as boolean
    dim i, index as integer

    index = WIDTH * line
    for i=0 to WIDTH-1
        if squares[index+i] = 0 then
            isLineFilled = false
            exit function
        end if
    next

    isLineFilled = true
end function

sub eraseFullLines()
    dim line, l2, r as integer
    for line = HEIGHT - 2 to 0 step -1
        if isLineFilled(line) then
            ' move lines one line down
            for l2 = line to 0 step -1
                for r=0 to WIDTH-1
                    squares[l2*WIDTH+r] = squares[(l2-1)*WIDTH+r]
                next
            next
            ' clear top line
            for r=1 to WIDTH-2
                squares[r] = 0
            next
            line++ ' test the same line again
        end if
    next
end sub

sub OnKeyDown(keyCode as integer)
    dim x, y, r as integer

    x = xpos
    y = ypos
    r = currentRot

    if keyCode = KEY_RIGHT then
        x++
    elseif keyCode = KEY_LEFT then
        x--
    elseif keyCode = KEY_DOWN then
        y++
    elseif keyCode = KEY_S then
        initGame
        exit sub
    elseif keyCode = KEY_CONTROL then
        r = (r + 1) mod 4
    end if

    if r &lt;&gt; currentRot then
        if drawBrick(x, y, r, 2) then
            currentRot = r
        elseif drawBrick(x-1, y, r, 2) then
            xpos = x - 1
            currentRot = r
        elseif drawBrick(x+1, y, r, 2) then
            xpos = x + 1
            currentRot = r
        else
            drawBrick(xpos, ypos, currentRot, 2)
        end if
    elseif x &lt;&gt; xpos or y &lt;&gt; ypos then
        if drawBrick(x, y, r, 2) then
            xpos = x
            ypos = y
        else
            drawBrick(xpos, ypos, currentRot, 2)
        end if
    end if

    display
end sub

sub OnExecPerField()
    counter++
    if counter &gt; 30 then
        counter = 0
        if drawBrick(xpos, ypos + 1, currentRot, 2) then
            ypos++
        else
            ' we hit the bottom
            drawBrick xpos, ypos, currentRot, 1
            eraseFullLines
            newBrick
        end if
        display
    end if
end sub
</pre>

</body>
</html>
